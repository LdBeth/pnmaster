 {r}←img writepnm(spec file);hdr;dta;x;y;tie
 ⍝ write img to file.
 ⍝ spec is one of 'P4' 'P5' 'P6', each stands
 ⍝ for PBM, PGM, PPM.
 :Select spec
 :Case 'P4' ⍝ pbm
     hdr←10,⍨⎕UCS ⍕x y←⌽⍴img
     dta←,(8×⌈8÷⍨x)↑⍤1⊢img
     dta←2⊥⍉dta⍴⍨(8÷⍨≢dta),8
 :Case 'P5' ⍝ pgm
     hdr←10 50 53 53 10,⍨⎕UCS ⍕⌽⍴img
     dta←,img
 :Case 'P6' ⍝ ppm
     hdr←10 50 53 53 10,⍨⎕UCS ⍕⌽1↓⍴img
     dta←,(¯1⌽⍳3)⍉img
 :Else
     ⎕SIGNAL⊂('EN' 6)('Message' 'Unknown file kind')
 :EndSelect
 tie←file ⎕NCREATE 0
 :Trap 0
     r←(spec,⎕UCS 10,hdr,dta)⎕NAPPEND tie 80
 :Else
     ⎕NUNTIE tie
     ⎕SIGNAL⊂('EN' 11)('Message' 'Error occured when write file')
 :EndTrap
 ⎕NUNTIE tie
